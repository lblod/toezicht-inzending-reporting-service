PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?typeBestuur ?kboNummer ?provincie ?naamBestuur ?besluitType ?status ?boekjaar ?besluitdatum ?ontvangstdatum ?ontvangstjaar ?ontvangstmaand ?ontvangstdag ?agendapunten ?linkLoket
WHERE {
  ?inzending a toezicht:InzendingVoorToezicht ;
      mu:uuid ?uuid ;
      adms:status <http://data.lblod.info/document-statuses/verstuurd> ;
      toezicht:decisionType/skos:prefLabel ?besluitType ;
      dct:subject ?bestuurseenheid ;
      nmo:sentDate ?ontvangstdatum .
      
  FILTER(?ontvangstdatum >= "{START}"^^xsd:dateTime && ?ontvangstdatum < "{END}"^^xsd:dateTime)

  ?bestuurseenheid a besluit:Bestuurseenheid .
  OPTIONAL {
      ?bestuurseenheid skos:prefLabel ?naamBestuur .
  }
  OPTIONAL {
      ?bestuurseenheid besluit:classificatie/skos:prefLabel ?typeBestuur .
  } 
  OPTIONAL {
      ?bestuurseenheid ext:kbonummer ?kboNummer .
  } 
  OPTIONAL {
      ?bestuurseenheid besluit:werkingsgebied/ext:inProvincie/rdfs:label ?provincie .
  }

  OPTIONAL {
      ?inzending toezicht:authenticityType/skos:prefLabel ?status .
  }

  OPTIONAL {
      ?inzending toezicht:temporalCoverage ?boekjaar .
  }

  OPTIONAL {
      ?inzending toezicht:sessionDate ?besluitdatum .
  }  

  OPTIONAL {
      ?inzending toezicht:agendaItemCount ?agendapunten .
  }
  
  BIND(STR(?ontvangstdatum) as ?ontvangstdatumStr)
  BIND(SUBSTR(?ontvangstdatumStr, 1, 4) as ?ontvangstjaar)
  BIND(SUBSTR(?ontvangstdatumStr, 6, 2) as ?ontvangstmaand)
  BIND(SUBSTR(?ontvangstdatumStr, 9, 2) as ?ontvangstdag)

  BIND(IRI(CONCAT("{DOMAIN_URL}/pub/toezicht/inzendingen/", ?uuid)) as ?linkLoket)
}
