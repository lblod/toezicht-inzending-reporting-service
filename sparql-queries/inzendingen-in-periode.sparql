PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>

SELECT DISTINCT ?typeBestuur as ?Type_Bestuur
                ?kboNummer as ?KBO_Nummer
                ?naamBestuur as ?Bestuur
                ?provincieLabel as ?Provincie
                ?besluitType as ?Type_Besluit
                ?status as ?Status
                ?boekjaar as ?Rapportjaar
                ?besluitdatum as ?Besluitdatum
                ?ontvangstdatum as ?Ontvangstdatum_en_tijdstip
                ?ontvangstjaar as ?Jaar_Ontvangst
                ?ontvangstmaand as ?Maand_Ontvangst
                ?ontvangstdag as ?Dag_Ontvangst
                ?agendapunten as ?Agendapunten
                (GROUP_CONCAT(DISTINCT ?linkLoket; SEPARATOR = ';') as ?Link_Loket)
WHERE {
  ?inzending a toezicht:InzendingVoorToezicht ;
      mu:uuid ?uuid ;
      adms:status <http://data.lblod.info/document-statuses/verstuurd> ;
      toezicht:decisionType/skos:prefLabel ?besluitType ;
      dct:subject ?bestuurseenheid ;
      nmo:sentDate ?ontvangstdatum .
      
  BIND(YEAR(?ontvangstdatum) as ?ontvangstjaar)
  BIND(MONTH(?ontvangstdatum) as ?ontvangstmaand)
  BIND(DAY(?ontvangstdatum) as ?ontvangstdag)

  FILTER(?ontvangstdatum >= "{START}"^^xsd:dateTime && ?ontvangstdatum < "{END}"^^xsd:dateTime)

  ?bestuurseenheid a besluit:Bestuurseenheid .
  OPTIONAL {
      ?bestuurseenheid skos:prefLabel ?naamBestuur .
  }
  OPTIONAL {
      ?bestuurseenheid besluit:classificatie/skos:prefLabel ?typeBestuur .
  } 
  OPTIONAL {
      ?bestuurseenheid ext:kbonummer ?kboNummer .
  } 
  OPTIONAL {
      ?bestuurseenheid besluit:werkingsgebied ?werkingsgebied .
      ?werkingsgebied ext:inProvincie* ?provincie .
      ?provincie ext:werkingsgebiedNiveau "Provincie" ; rdfs:label ?provincieLabel .
  }

  OPTIONAL {
      ?inzending toezicht:authenticityType/skos:prefLabel ?status .
  }

  OPTIONAL {
      ?inzending toezicht:temporalCoverage ?boekjaar .
  }

  OPTIONAL {
      ?inzending toezicht:sessionDate ?besluitdatum .
  }  

  OPTIONAL {
      ?inzending toezicht:agendaItemCount ?agendapunten .
  }
  
  OPTIONAL {
      ?inzending nie:hasPart ?file .
      ?downloadFile nie:dataSource ?file ;
                    nfo:fileName ?fileName
  }

  BIND(IRI(CONCAT("{DOMAIN_URL}/toezicht/bestanden/", ?fileName)) as ?linkLoket)
}
